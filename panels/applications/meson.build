panels_list += cappletname
desktop = 'regolith-@0@-panel.desktop'.format(cappletname)

i18n.merge_file(
         type : 'desktop',
        input : desktop + '.in',
       output : desktop,
       po_dir : po_dir,
      install : true,
  install_dir : control_center_desktopdir
)

sources = files(
  'cc-applications-panel.c',
  'cc-applications-row.c',
  'cc-info-row.c',
  'globs.c',
  'search.c',
  'utils.c',
)

sources += gnome.compile_resources(
  'cc-' + cappletname + '-resources',
  cappletname + '.gresource.xml',
  c_name : 'cc_' + cappletname,
  export : true
)

deps = common_deps

if enable_snap
  deps += snapd_glib_deps
  sources += files('cc-snap-row.c')
endif

if enable_malcontent
  deps += malcontent_dep
endif

panels_libs += static_library(
           cappletname,
              sources : sources,
  include_directories : [ top_inc, common_inc ],
         dependencies : deps,
               c_args : cflags
)

# subdir('icons')
